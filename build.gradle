import org.apache.tools.ant.taskdefs.condition.Os

group 'by.korozhan.news'
version '1.0-SNAPSHOT'

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.akhikhl.gretty:gretty:+'
    }
}

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'org.akhikhl.gretty'

gretty {
    httpPort = 9876
    contextPath = '/'
}

sourceCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

ext {
    springdataMongodbVersion = '2.0.5.RELEASE'
    springframeworkVersion = '5.0.4.RELEASE'
    servletapiVersion = '4.0.0'
    jacksonVersion = '2.9.4'
    modelMapperVersion = '0.7.4'
    log4jVersion = '1.2.17'
    slf4jVersion = '1.7.25'
    swaggerVersion = '2.7.0'
    junitVersion = '4.12'
    jaywayVersion = '2.4.0'
}

dependencies {
    compile "org.springframework.data:spring-data-mongodb:${springdataMongodbVersion}"
    compile "org.springframework:spring-webmvc:${springframeworkVersion}"
    providedCompile "javax.servlet:javax.servlet-api:${servletapiVersion}"
    compile "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    compile "org.modelmapper:modelmapper:${modelMapperVersion}"
    compile "org.springframework:spring-test:${springframeworkVersion}"
    compile "log4j:log4j:${log4jVersion}"
    compile "org.slf4j:slf4j-api:${slf4jVersion}"
    compile "org.slf4j:slf4j-log4j12:${slf4jVersion}"
    compile "io.springfox:springfox-swagger2:${swaggerVersion}"
    compile "io.springfox:springfox-swagger-ui:${swaggerVersion}"
    testCompile "junit:junit:${junitVersion}"
    testCompile "com.jayway.jsonpath:json-path:${jaywayVersion}"
}

def npmName = Os.isFamily(Os.FAMILY_WINDOWS) ? 'npm.cmd' : 'npm';

task npmInstall(type: Exec) {
    description 'Install client related packages'
    workingDir './news-client'
    commandLine npmName, 'install'
}

task buildClient(type: Exec, dependsOn: npmInstall) {
    description 'Builds news client code'
    workingDir './news-client'
    commandLine npmName, 'run', 'build'
}

task copyClient(type: Copy, dependsOn: buildClient) {
    description 'Copies client build code to webapp'
    from './news-client/dist'
    into './src/main/webapp'
}

tasks.war.dependsOn copyClient